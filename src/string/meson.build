# Copyright (c) 2022 Guilherme Janczak <guilherme.janczak@yandex.com>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

# The keys inside the string dictionary are the names of the functions.
# The value is an array. Here are the uses of the 3 members of the arrays:
# [0] = 'filename of the source file that implements the function'
# [1] = 'filename of the manual of the function'
# [2] = is the function needed? true/false
#
string = {
  'strlcpy': ['strlcpy.c', 'strlcpy.3'],
  'strlcat': ['strlcat.c', 'strlcpy.3'],
}

# set [2] in each array of string
foreach func, info : string
  string += {func: string[func] + [not cc.has_function(func)]}
endforeach

string_h_data = configuration_data()
string_needed = false
string_funcs = []
string_src = []
string_man = []
string_inc = []
string_incdir = []

foreach func, info : string
  string_h_data.set(func, info[2].to_int())
  if info[2] # If func is needed
    string_funcs += func # build up a database of functions in the library
    string_src += files(info[0])
    string_man += files(info[1])
    string_needed = true
  endif
endforeach

if string_needed
  string_inc = configure_file(input: 'string.h.in',
                               output: 'string.h',
                               configuration: string_h_data)
  string_incdir = include_directories('.')
endif
